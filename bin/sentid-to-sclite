#!/usr/local/bin/gawk -f
#
# sentid-to-sclite --
#	convert sentid transcription format to sclite 'trn' format
#
# $Header: /home/srilm/devel/utils/src/RCS/sentid-to-sclite,v 1.2 1998/01/12 18:33:45 stolcke Exp $
#
# i.e.:
#	sentid word1 word2 ....
#
# becomes
#
#	word1 word2 ... (sentid)
#
# The sentid is formatted to contain exactly one underscore,
# as sclite uses the first portion of the id as a speaker label to
# group results.
#
{
    sentid = $1;
    $1 = "";

    # reformat sentid
    # <conv>_<channel>_<utterance> -> <conv><channel>_<utterance>
    sub("[-_]A", "A", sentid);
    sub("[-_]B", "B", sentid);
    sub("[-_]ch1", "ch1", sentid);
    sub("[-_]ch2", "ch2", sentid);

    # reformat sentid

    # remove underscore after corpus tag, if any
    if (sentid ~ /^[a-z][a-z]*[-_][0-9]/) {
	sub("[-_]", "", sentid);
    }

    # <conv>_<channel>_<utterance> -> <conv><channel>_<utterance>
    sub("[-_]A", "A", sentid);
    sub("[-_]B", "B", sentid);
    sub("[-_]ch1", "ch1", sentid);
    sub("[-_]ch2", "ch2", sentid);

    print $0, "(" sentid ")";
}
