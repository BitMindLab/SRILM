#
#    File:   CSHRC
#    Author: ***PSI***
#    Date:   Thu Dec 17 14:50:57 1992
#
#    Description:
#       This file contains path, environment variable, and alias
#       definitions for use with the DECIPHER System.  To use these
#       definitions, add the following line to your ".cshrc" file:
#
#	     [setenv DECIPHER <Pathname>]  --  Optional.
#            source ~decipher/system/common/CSHRC
#
#	If you do not supply a value for the "DECIPHER" environment
#	variable prior to source'ing this file, the value will default
#	to the location of the latest frozen version of the DECIPHER
#	System.
#
#    Copyright (c) 1994, SRI International.  All Rights Reserved.
#
#    RCS ID: $Id: CSHRC,v 1.5 1995/04/22 01:21:42 tmk Exp $
#
#    $Log: CSHRC,v $
# Revision 1.5  1995/04/22  01:21:42  tmk
# Updated to use the new machine-type command.
#
# Revision 1.4  1994/02/03  23:53:00  decipher
# Updated to allow setting the "DECIPHER" environment externally, or else to default
# to the latest frozen version of the system.
#
# Revision 1.3  1993/09/27  09:04:28  decipher
# Updated to use the official location for the DECIPHER System.
#
# Revision 1.2  1993/09/09  20:56:52  decipher
# Added the unsupported DECIPHER tools binary directory to the path.
#
# Revision 1.1  1993/01/25  02:36:36  decipher
# Initial revision
#
#

#
# Note:  Your path should contain "/bin" and "/usr/local/bin"
#         before this file is "source"'d.
#

# If the user hasn't already set the value of the DECIPHER System
#  directory, default to the latest frozen version.
if (! $?DECIPHER) then
    setenv DECIPHER     /home/decipher/frozen/latest
endif

# Determine the machine type
if (! $?MACHINE_TYPE) then
   if (-e $DECIPHER/bin/machine-type) then 
	setenv MACHINE_TYPE `$DECIPHER/bin/machine-type`
   else
	setenv MACHINE_TYPE `mach`
   endif
endif

# Shorthand for DECIPHER (TM) directories.
setenv DI	    $DECIPHER/include
setenv DL	    $DECIPHER/lib/$MACHINE_TYPE
setenv DLG	    $DECIPHER/lib/$MACHINE_TYPE\_g
setenv DLP	    $DECIPHER/lib/$MACHINE_TYPE\_p
setenv DB	    $DECIPHER/bin/$MACHINE_TYPE
setenv DBG	    $DECIPHER/bin/$MACHINE_TYPE\_g
setenv DBP	    $DECIPHER/bin/$MACHINE_TYPE\_p
setenv DBX	    $DECIPHER/bin/$MACHINE_TYPE\_x

# Set the path shared by all machine types
set path = ($path $DECIPHER/bin $DECIPHER/unsupported/bin /usr/local/bin/rcs)

# Do machine type-dependent processing
if ($MACHINE_TYPE == sparc) then

   # Add sparc-specific directories to the path.
   # (There are none for now.)

   # Set up the man path to include other man page locations
   setenv MANPATH    /usr/man:/usr/local/man:/usr/lib/X11/man:/usr/local/bin/rcs/man

else # (Assume the machine type is "mips")

   # Add mips-specific directories to the path.
   # (There are none for now.)

endif


# Define aliases.


# Make targets with optimization.
alias make	'gnumake'             

# Make targets for debugging.
alias makeg	'gnumake OPTION=_g'

# Make targets for profiling.
alias makep	'gnumake OPTION=_p'

# Make targets for export.
alias makex	'gnumake OPTION=_x'
